import "util.scrypt";
import "serializer.scrypt";

/*
 * test state serializing/deserializing
 */
contract StateSerializer {
	public function mutate(SigHashPreimage txPreimage, int amount) {
        require(Tx.checkPreimage(txPreimage));

        bytes scriptCode = Util.scriptCode(txPreimage);
		int scriptLen = len(scriptCode);

		// locking script: code + opreturn + data(state + state_len)
		
		// read state length
		bytes lb = scriptCode[scriptLen - Util.StateLen :];
		Reader lr = new Reader(lb);
		int stateLen = lr.readInt();
		require(stateLen == 7);
		
		// read state
		int stateStart = scriptLen - stateLen - Util.StateLen;
		bytes state = scriptCode[stateStart :];
		Reader r = new Reader(state);
		auto counter = r.readInt();
		require(counter == 11);
		auto b = r.readBytes();
		require(b == b'1234');
		bool flag = r.readBool();
		require(flag);

		// serialize state
    bytes ext = b'0102030405060708090001020304050607080900010203040506070809000102030405060708090001020304050607080900010203040506070809000102030405060708090001020304050607080900';
		bytes stateBuf = Writer.writeInt(counter + 1) + Writer.writeBytes(b + b'ff') + Writer.writeBool(!flag) + Writer.writeBytes(ext);
		// serialize state size
		bytes lenBuf = Writer.writeBytes(Util.encodeStateSize(stateBuf));
		
		bytes scriptCode_ = scriptCode[: stateStart] + stateBuf + lenBuf;
		bytes output = Util.buildOutput(scriptCode_, amount);
		require(hash256(output) == Util.hashOutputs(txPreimage));
    }
}